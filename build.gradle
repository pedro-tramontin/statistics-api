plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.0.5.RELEASE'
    id 'org.springframework.boot' version '2.0.2.RELEASE'
    id 'org.sonarqube' version '2.6.2'
    id 'com.palantir.docker' version '0.13.0'
}

apply plugin: "jacoco"

group 'br.com.pedront.n26'
version '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.16.22'
    compileOnly 'org.springframework.boot:spring-boot-configuration-processor'

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'io.springfox:springfox-swagger-ui:2.7.0'
    compile 'io.springfox:springfox-swagger2:2.7.0'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile("org.springframework.restdocs:spring-restdocs-mockmvc")
    testCompile 'org.yaml:snakeyaml:1.19'

}

jacoco {
    toolVersion '0.8.1'
}

jacocoTestReport {
    reports {
        csv.enabled false
        xml.enabled false

        html.enabled true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect{
            fileTree(dir: it, exclude: ['**/com/n26/statistics/api/Application**',
                                        '**/com/n26/statistics/api/controller/response/**',
                                        '**/com/n26/statistics/api/controller/request/**'])
        })
    }
}

test {
    jacoco {
        append = false
        excludes += ['**/com.n26.statistics.api.Application**',
                    '**/com.n26.statistics.api.controller.response/**',
                    '**/com.n26.statistics.api.controller.request/**']
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'pedront-github'
        property 'sonar.login', 'ae8655489f8bdee64af2681702f09d2a40f0c2ad'
        property 'sonar.coverage.exclusions', ['**/com/n26/statistics/api/Application**',
                                               '**/com/n26/statistics/api/controller/response/**',
                                               '**/com/n26/statistics/api/controller/request/**']
    }
}

docker {
    dependsOn build
    name 'pedront/statistics-api'
    tags 'latest'
    dockerfile file('Dockerfile')
    files bootJar.archivePath
    buildArgs(['JAR_FILE': "${bootJar.archiveName}"])
}